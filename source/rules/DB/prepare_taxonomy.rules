localrules:     krona_taxonomy, download_taxdump, tango_build_sqlite, tango_download, tango_format_nr, tango_format_uniref

rule download_taxdump:
    output:
        nodes = opj(config["resource_path"],"taxonomy","nodes.dmp"),
        names = opj(config["resource_path"],"taxonomy","names.dmp"),
        tar = opj(config["resource_path"],"taxonomy","taxdump.tar.gz")
    params:
        taxdir = opj(config["resource_path"], "taxonomy")
    shadow: "minimal"
    shell:
        """
        curl -o {output.tar} ftp://ftp.ncbi.nlm.nih.gov/pub/taxonomy/taxdump.tar.gz
        tar -xf {output.tar} -C {params.taxdir}
        touch {output.tar}
        """

rule krona_taxonomy:
    input:
        opj(config["resource_path"],"taxonomy","taxonomy.sqlite"),
        opj(config["resource_path"],"taxonomy","taxdump.tar.gz"),
        opj(config["resource_path"],"taxonomy","nodes.dmp"),
        opj(config["resource_path"],"taxonomy","names.dmp")
    output:
        opj(config["resource_path"],"krona","taxonomy.tab")
    params:
        taxdir = opj(config["resource_path"],"taxonomy")
    shell:
        """
        ktUpdateTaxonomy.sh --only-build --preserve {params.taxdir}
        mv {params.taxdir}/taxonomy.tab {output}
        """

rule tango_build_sqlite:
    input:
        opj(config["resource_path"],"taxonomy","taxdump.tar.gz")
    output:
        opj(config["resource_path"],"taxonomy","taxonomy.sqlite"),
        opj(config["resource_path"],"taxonomy","taxonomy.sqlite.traverse.pkl")
    params:
        taxdir = opj(config["resource_path"],"taxonomy")
    shell:
        """
        tango download taxonomy -t {params.taxdir}
        """

rule tango_download:
    output:
        fasta = temp(opj(config["resource_path"], "{db}", "{db}.fasta.gz"))
    params:
        dldir = opj(config["resource_path"], "{db}"),
        taxdir = opj(config["resource_path"], "taxonomy")
    shell:
        """
        tango download {wildcards.db} --tmpdir $TMPDIR -d {params.dldir} -t {params.taxdir} --skip_idmap
        """

rule tango_download_nr_idmap:
    output:
        idmap = opj(config["resource_path"], "nr", "prot.accession2taxid.gz")
    params:
        dldir = opj(config["resource_path"], "nr")
    shell:
        """
        tango download idmap -d {params.dldir}
        """

rule tango_format_uniref:
    input:
        fasta = opj(config["resource_path"], "{db}", "{db}.fasta.gz")
    output:
        fasta = opj(config["resource_path"], "{db}", "{db}.reformat.fasta.gz"),
        idmap = opj(config["resource_path"], "{db}", "prot.accession2taxid.gz")
    params:
        tmpdir = config["scratch_path"]
    shell:
        """
        tango format -m {output.idmap} --tmpdir {params.tmpdir} {input.fasta} {output.fasta}
        """

rule tango_format_nr:
    input:
        fasta = opj(config["resource_path"], "nr", "nr.fasta.gz")
    output:
        fasta = opj(config["resource_path"], "nr", "nr.reformat.fasta.gz")
    params:
        tmpdir = config["scratch_path"]
    shell:
        """
        tango format --tmpdir {params.tmpdir} {input.fasta} {output.fasta}
        """

rule tango_update:
    input:
        idmap = opj(config["resource_path"], "{db}", "prot.accession2taxid.gz")
    output:
        flag = touch(opj(config["resource_path"], "{db}", "updated"))
    run:
        import os
        if os.path.exists(opj(config["resource_path"], wildcards.db, "idmap.tsv.gz")):
            idfile = opj(config["resource_path"], wildcards.db, "idmap.tsv.gz")
            newfile = "{}.new.gz".format((input.idmap).rstrip(".gz"))
            shell("tango update {input.idmap} {idfile} {newfile}")
            shell("mv {newfile} {input.idmap}")


rule tango_build:
    input:
        flag = opj(config["resource_path"], "{db}", "updated"),
        fasta = opj(config["resource_path"], "{db}", "{db}.reformat.fasta.gz"),
        nodes = opj(config["resource_path"],"taxonomy","nodes.dmp"),
        idmap = opj(config["resource_path"], "{db}", "prot.accession2taxid.gz")
    output:
        opj(config["resource_path"],"{db}","diamond.dmnd")
    threads: config["diamond_threads"]
    resources:
        runtime = lambda wildcards, attempt: attempt**2*60*10
    shell:
         """
         tango build -d {output[0]} -p {threads} {input.fasta} {input.idmap} {input.nodes} 
         """

